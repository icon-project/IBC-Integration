buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("foundation.icon:gradle-javaee-plugin:$javaeePluginVersion")
        classpath 'com.squareup.wire:wire-gradle-plugin:4.5.0'
    }
}

def scoreIntegrationTest = new HashMap<String, String>()

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        google()
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterApiVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jupiterEngineVersion"    
}


subprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    apply plugin: 'java'
    apply plugin: 'foundation.icon.javaee'
    apply plugin: 'jacoco'

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    sourceSets {
        intTest {
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
            java {
            }
        }
    }

    dependencies {
        compileOnly("foundation.icon:javaee-api:$javaeeVersion")
        implementation("foundation.icon:javaee-scorex:$scorexVersion")

        annotationProcessor("foundation.icon:javaee-score-client:$scoreClientVersion")
        compileOnly("foundation.icon:javaee-score-client:$scoreClientVersion")

        testImplementation 'com.google.protobuf:protobuf-javalite:3.13.0'
        testImplementation 'foundation.icon:javaee-rt:0.9.3'
        testImplementation("org.mockito:mockito-core:$mockitoCoreVersion")
        testImplementation("org.mockito:mockito-inline:$mockitoCoreVersion")
        testImplementation("foundation.icon:javaee-unittest:$javaeeUnittestVersion")
        testAnnotationProcessor("foundation.icon:javaee-score-client:$scoreClientVersion")
        testImplementation("foundation.icon:javaee-score-client:$scoreClientVersion")
        testImplementation("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
        testImplementation("foundation.icon:icon-sdk:$iconsdkVersion")
        testImplementation("org.junit.jupiter:junit-jupiter-api:$jupiterApiVersion")
        testImplementation("org.junit.jupiter:junit-jupiter-params:$jupiterParamsVersion")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$jupiterEngineVersion")
    }

    configurations {
        intTestImplementation.extendsFrom testImplementation
        intTestAnnotationProcessor.extendsFrom testAnnotationProcessor
        intTestRuntimeOnly.extendsFrom testRuntimeOnly
    }

    scoreIntegrationTest.put('env.props', new File(project(':test-lib').projectDir, 'conf/env.props'))

    afterEvaluate { project ->
        scoreIntegrationTest.put(project.name, project.getTasks().getByName("optimizedJar").outputJarName)
    }


    // need to add this option to retrieve formal parameter names
    compileJava {
        options.compilerArgs += ['-parameters']
    }
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.5"
    // reportsDir = file("$buildDir/reports/jacoco")
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml {
            enabled true
        }
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }

    inputs.files(project(":ibc").tasks.jacocoTestReport.outputs.files)
    inputs.files(project(":tendermint").tasks.jacocoTestReport.outputs.files)
    inputs.files(project(":xcall").tasks.jacocoTestReport.outputs.files)

    sourceDirectories.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(subprojects.sourceSets.main.output)
}

codeCoverageReport.dependsOn {
    subprojects*.test
}


gradle.taskGraph.whenReady { taskGraph ->
    taskGraph.getAllTasks().eachWithIndex { task, n ->
        if (task.name == 'integrationTest') {
            scoreIntegrationTest.each { key, val -> task.systemProperty key, val }
        }
    }
}
